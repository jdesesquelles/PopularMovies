apply plugin: 'com.android.application'



android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "co.fabrk.popmovies"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
    }

    // If you need to add more flavors, consider using flavor dimensions.
    productFlavors {
//        mock {
//            applicationIdSuffix ".mock"
//        }
        prod {

        }
    }
    buildTypes {
        debug {
            // Run code coverage reports by default on debug builds.
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }


    buildTypes.each {
        it.buildConfigField 'String', 'TMDB_API_KEY', TMDB_API_KEY
    }
    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:design:$rootProject.ext.designVersion"
    compile "com.android.support:appcompat-v7:$rootProject.ext.appcompatV7Version"
    compile "com.squareup.picasso:picasso:$rootProject.ext.picassoVersion"
    compile "com.android.support:support-v4:$rootProject.ext.supportV4Version"
    compile "com.android.support:recyclerview-v7:$rootProject.ext.recyclerviewVersion"
    compile "com.google.apis:google-api-services-youtube:$rootProject.ext.gasyoutubeVersion"
    compile "com.android.support:palette-v7:$rootProject.ext.paletteV7Version"
    compile files('libs/YouTubeAndroidPlayerApi.jar')
    compile "com.jakewharton:butterknife:$rootProject.ext.butterknifeVersion"
    // Dependencies for local unit tests
    testCompile "junit:junit:$rootProject.ext.junitVersion"
}

/*
Resolves dependency versions across test and production APKs, specifically, transitive
dependencies. This is required since Espresso internally has a dependency on support-annotations.
*/
configurations.all {
    resolutionStrategy.force "com.android.support:support-annotations:$rootProject.ext.supportAnnotationsVersion"
}






